Value INTERFACE ([a-zA-Z\-]+\s*(\d+(/\d+)*)?)
Value STATUS (.*)
Value IFACE_INDEX (\d+)
Value MAC_ADDRESS ([a-zA-Z0-9]{2}(:[a-zA-Z0-9]{2}){5})
Value DESCRIPTION (.*)
Value MTU (\d+)
Value UPTIME (.*)
Value LINK_TYPE (.*)
Value IFACE_IN_PO_NUM (\d+)
Value ACTIVE_IFACE_IN_PO_NUM (\d+)
Value IFACE_IN_PO_F1 ([a-zA-Z\-]+\s*(\d+(/\d+)*)?(,[a-zA-Z\-]+\s*(\d+(/\d+)*)?)*)
Value List IFACE_IN_PO_F2 ([a-zA-Z\-]+\s*(\d+(/\d+)*)?)
Value List IFACE_IN_PO_BANDWIDTH (\d+[a-zA-Z]+)
Value List IFACE_IN_PO_STATUS (.*)
Value SUM_BANDWIDTH ([a-zA-Z0-9]+)
Value INPUT_RATE (-?\d+)
Value OUTPUT_RATE (-?\d+)
Value INPUT_PACKETS (-?\d+)
Value BYTES_RECEIVED (-?\d+)
Value BROADCASTS_NUM (-?\d+)
Value MULTICASTS_NUM (-?\d+)
Value INPUT_ERR_NUM (-?\d+)
Value FCS_NUM (-?\d+)
Value ALIGNMENTS_NUM (-?\d+)
Value OVERSIZE_NUM (-?\d+)
Value INTERNAL_MAC_NUM (-?\d+)
Value PAUSE_FRAMES_RECEIVED (-?\d+)
Value OUTPUT_PACKETS (-?\d+)
Value BYTES_SENT (-?\d+)
Value OUTPUT_ERR_NUM (-?\d+)
Value COLLISIONS_NUM (-?\d+)
Value EXCESSIVE_COLLISIONS_NUM (-?\d+)
Value LATE_COLLISIONS_NUM (-?\d+)
Value PAUSE_FRAMES_SENT (-?\d+)
Value SYMBOL_ERR_NUM (-?\d+)
Value CARRIER_NUM (-?\d+)
Value SQE_TEST_ERR_NUM (-?\d+)


Start
  ^\s*Channel\s+Ports\s*$$ -> Firmware1
  ^\s*${INTERFACE}\s+is\s+${STATUS}\s+\(.*\)\s*$$ -> Firmware2

Firmware1
  ^\s*-+(\s+-+)*\s*$$
  ^\s*${INTERFACE}\s+(Active|Non-candidate):\s*${IFACE_IN_PO_F1}\s*$$ -> Record Firmware1
  ^\s*$$
  ^. -> Error

Firmware2
  ^\s*Interface\s+index\s+is\s+${IFACE_INDEX}\s*$$
  ^\s*.*MAC\s+address\s+is\s+${MAC_ADDRESS}\s*$$
  ^\s*Description:\s+${DESCRIPTION}\s*$$
  ^\s*Interface\s+MTU\s+is\s+${MTU}\s*$$
  ^\s*Link\s+is\s+up\s+for\s+${UPTIME}\s*$$
  ^\s*Link\s+aggregation\s+type\s+is\s+${LINK_TYPE}\s*$$
  ^\s*No.\s+of\s+members\s+in\s+this\s+port-channel:\s+${IFACE_IN_PO_NUM}\s+\(active\s+${ACTIVE_IFACE_IN_PO_NUM}\)\s*$$
  ^\s*${IFACE_IN_PO_F2},.*,\s*${IFACE_IN_PO_BANDWIDTH}\s+\(${IFACE_IN_PO_STATUS}\)\s*$$
  ^\s*Active\s+bandwidth\s+is\s+${SUM_BANDWIDTH}\s*$$
  ^\s*15\s+second\s+input\s+rate\s+is\s+${INPUT_RATE}\s+Kbit/s\s*$$
  ^\s*15\s+second\s+output\s+rate\s+is\s+${OUTPUT_RATE}\s+Kbit/s\s*$$
  ^\s*${INPUT_PACKETS}\s+packets\s+input,\s+${BYTES_RECEIVED}\s+bytes\s+received\s*$$
  ^\s*${BROADCASTS_NUM}\s+broadcasts,\s+${MULTICASTS_NUM}\s+multicasts\s*$$
  ^\s*${INPUT_ERR_NUM}\s+input\s+errors,\s+${FCS_NUM}\s+FCS,\s+${ALIGNMENTS_NUM}\s+alignment\s*$$
  ^\s*${OVERSIZE_NUM}\s+oversize,\s+${INTERNAL_MAC_NUM}\s+internal\s+MAC\s*$$
  ^\s*${PAUSE_FRAMES_RECEIVED}\s+pause\s+frames\s+received\s*$$
  ^\s*${OUTPUT_PACKETS}\s+packets\s+output,\s+${BYTES_SENT}\s+bytes\s+sent\s*$$
  ^\s*${OUTPUT_ERR_NUM}\s+output\s+errors,\s+${COLLISIONS_NUM}\s+collisions\s*$$
  ^\s*${EXCESSIVE_COLLISIONS_NUM}\s+excessive\s+collisions,\s+${LATE_COLLISIONS_NUM}\s+late\s+collisions\s*$$
  ^\s*${PAUSE_FRAMES_SENT}\s+pause\s+frames\s+transmitted\s*$$
  ^\s*${SYMBOL_ERR_NUM}\s+symbol\s+errors,\s+${CARRIER_NUM}\s+carrier,\s+${SQE_TEST_ERR_NUM}\s+SQE\s+test\s+error\s*$$ -> Record Firmware2
  ^\s*$$
